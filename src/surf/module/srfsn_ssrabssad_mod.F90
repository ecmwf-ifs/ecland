MODULE SRFSN_SSRABSSAD_MOD
CONTAINS
SUBROUTINE SRFSN_SSRABSSAD(KIDIA,KFDIA,KLON,KLEVSN,&
 & LLNOSNOW,PFRTI,PSSRFLTI5,&
 & PSSNM1M5,PRSNM1M5,&
 & YDSOIL,YDCST,&
 & PSNOTRS5,&
! Perturbation
 & PSSRFLTI, PSSNM1M,&
 & PSNOTRS )


USE PARKIND1 , ONLY : JPIM, JPRB
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK, JPHOOK

USE YOS_SOIL , ONLY : TSOIL 
USE YOS_CST  , ONLY : TCST

USE ABORT_SURF_MOD

! (C) Copyright 2020- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

!**** *SRFSN_SSRABSSAD* - Shortwave radiation absorption by snow (adjoint)
!     PURPOSE.
!     --------
!          THIS ROUTINE COMPUTES SW ABSORBED BY EACH SNOWPACK LAYER 

!**   INTERFACE.
!     ----------
!          *SRFSN_SSRABSSAD* IS CALLED FROM *SURFTSTPSAD*.

!     PARAMETER   DESCRIPTION                                    UNITS
!     ---------   -----------                                    -----

!     INPUT PARAMETERS (INTEGER):
!    *KIDIA*      START POINT
!    *KFDIA*      END POINT
!    *KLON*       NUMBER OF POINTS LON
!    *KLEVSN*     NUMBER OF MAX VERTICAL SNOW LAYERS


!     INPUT PARAMETERS (REAL):
!    *PFRTI*      TILE FRACTION                                     

!     INPUT PARAMETERS (LOGICAL):
!    *LLNOSNOW*     SNOW/NO-SNOW MASK (TRUE IF NO-SNOW)

!     INPUT PARAMETERS AT T-1 OR CONSTANT IN TIME (REAL):
!    Trajectory  Perturbation  Description                          Unit
!     
!    *PSSNM1M5*  *PSSNM1M*    SNOW WATER EQUIVALENT T-1            kg/m**2
!    *PRSNM1M5*      -        SNOW DENSITY          T-1            kg/m**3
!    *PSSRFLTI5* *PSSRFLTI*   TILED SHORTWAVE RADIATION AT SURFACE W/m**2

!     OUTPUT PARAMETERS AT T+1 (UNFILTERED,REAL):
!    *PSNOTRS5*    *PSNOTRS*  SOLAR RADIATION ABSORBED BY EACH SNOW LAYER W/m**2


!     METHOD.
!     -------
          

!     EXTERNALS.
!     ----------
!          NONE.

!     REFERENCE.
!     ----------
!          

!     Modifications:
!     Original   G. Arduini      ECMWF     04/12/2015

!     ------------------------------------------------------------------

IMPLICIT NONE

! Declaration of arguments 
INTEGER(KIND=JPIM), INTENT(IN)   :: KIDIA
INTEGER(KIND=JPIM), INTENT(IN)   :: KFDIA
INTEGER(KIND=JPIM), INTENT(IN)   :: KLON
INTEGER(KIND=JPIM), INTENT(IN)   :: KLEVSN
LOGICAL           , INTENT(IN)   :: LLNOSNOW(:) 

REAL(KIND=JPRB)   , INTENT(IN)   :: PFRTI(:,:)
! Traj input
REAL(KIND=JPRB),    INTENT(IN)   :: PSSRFLTI5(:,:)
REAL(KIND=JPRB)   , INTENT(IN)   :: PSSNM1M5(:,:)
REAL(KIND=JPRB)   , INTENT(IN)   :: PRSNM1M5(:,:)

TYPE(TSOIL)       , INTENT(IN)   :: YDSOIL
TYPE(TCST)        , INTENT(IN)   :: YDCST

! Traj output
REAL(KIND=JPRB)   , INTENT(OUT)  :: PSNOTRS5(:,:)

!Perturbation
REAL(KIND=JPRB),    INTENT(INOUT)   :: PSSRFLTI(:,:)
REAL(KIND=JPRB)   , INTENT(INOUT)   :: PSSNM1M(:,:)

REAL(KIND=JPRB)   , INTENT(INOUT)  :: PSNOTRS(:,:)

! Local variables 
REAL(KIND=JPRB)    :: ZDSN5(KLEVSN),           ZDSN(KLEVSN)
REAL(KIND=JPRB)    :: ZSNOTRSTMP5(KLEVSN+1),   ZSNOTRSTMP(KLEVSN+1)
REAL(KIND=JPRB)    :: ZSNSOABS5(KLON,KLEVSN),ZSNSOABS(KLON,KLEVSN)
REAL(KIND=JPRB)    :: ZSNSOABS55(KLON,KLEVSN), ZSNSOABS75(KLON,KLEVSN)
REAL(KIND=JPRB)    :: ZSNQRAD5(KLON,KLEVSN)
REAL(KIND=JPRB)    :: ZSNQRAD55(KLON,KLEVSN), ZSNQRAD75(KLON,KLEVSN)
REAL(KIND=JPRB)    :: ZSNQRAD(KLEVSN)

REAL(KIND=JPRB)    :: ZSNEXTCOEFF5(KLON,KLEVSN)
REAL(KIND=JPRB)    :: ZGSNS5

REAL(KIND=JPRB)    :: ZFRSN(KLON)
REAL(KIND=JPRB)    :: ZFRSN_TOT
REAL(KIND=JPRB)    :: ZEPSILON
INTEGER(KIND=JPIM) :: KLACT
INTEGER(KIND=JPIM) :: KSNTILES


INTEGER(KIND=JPIM) :: JL,JK,JT,JTILE
REAL(KIND=JPHOOK)  :: ZHOOK_HANDLE

!! INCLUDE FUNCTIONS

!    -----------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SRFSN_SSRABSSAD_MOD:SRFSN_SSRABSSAD',0,ZHOOK_HANDLE)

!    -----------------------------------------------------------------
ASSOCIATE(RDSNMAX=>YDSOIL%RDSNMAX, SSAG1=>YDSOIL%SSAG1, SSAG2=>YDSOIL%SSAG2, &
         & SSAG3=>YDSOIL%SSAG3, SSAGSNSMAX=>YDSOIL%SSAGSNSMAX, SSASNEXTMIN=>YDSOIL%SSASNEXTMIN, &
         & SSASNEXTMAX=>YDSOIL%SSASNEXTMAX, SSASNEXTCNST=>YDSOIL%SSASNEXTCNST )

KSNTILES=2 ! nr of snow tiles
ZEPSILON=1._JPRB*EPSILON(ZEPSILON)

KLACT=1
ZSNSOABS55(:,1:KLEVSN) = 0._JPRB
ZSNSOABS75(:,1:KLEVSN) = 0._JPRB
DO JT=1,KSNTILES
  SELECT CASE(JT)
  CASE(1)
    JTILE=5
    ZFRSN(KIDIA:KFDIA)=PFRTI(KIDIA:KFDIA,JTILE)
  CASE(2)
    JTILE=7
    ZFRSN(KIDIA:KFDIA)=PFRTI(KIDIA:KFDIA,JTILE)
  END SELECT
  DO JL=KIDIA,KFDIA
    IF (LLNOSNOW(JL)) THEN 
      PSNOTRS5(JL,1:KLEVSN+1) = 0._JPRB
    ELSE
  
  !! Preparation
      IF ((PFRTI(JL,5)+PFRTI(JL,7))>YDSOIL%RFRTINY) THEN
        ZFRSN_TOT=PFRTI(JL,5)+PFRTI(JL,7)
      ELSE
        ZFRSN_TOT=YDSOIL%RFRTINY
      ENDIF
      DO JK=1,KLEVSN
        IF (PSSNM1M5(JL,JK) > ZEPSILON ) KLACT=JK
      ENDDO
      ZSNOTRSTMP5(1:KLEVSN+1)=0._JPRB
      ZSNQRAD5(JL,1:KLEVSN)  = 0._JPRB
      ZSNSOABS5(JL,1:KLEVSN) = 0._JPRB
      ZSNEXTCOEFF5(JL,1:KLEVSN)=25._JPRB

      DO JK=1, KLACT
        IF (PSSNM1M5(JL,JK)/(ZFRSN_TOT*PRSNM1M5(JL,JK))<RDSNMAX) THEN
          ZDSN5(JK) = PSSNM1M5(JL,JK)/(ZFRSN_TOT*PRSNM1M5(JL,JK))
        ELSE
          ZDSN5(JK) = RDSNMAX
        ENDIF
        ! grain size from Anderson 1976 (parameter)
        ZGSNS5=MIN(SSAGSNSMAX,(SSAG1 + SSAG3*PRSNM1M5(JL,JK)**(4._JPRB) ))
        ! snow extinction coeff from Jordan 1991
        ZSNEXTCOEFF5(JL,JK)=MAX(SSASNEXTMIN, MIN(SSASNEXTMAX, SSASNEXTCNST*PRSNM1M5(JL,JK)/ZGSNS5**0.5_JPRB ) )
      ENDDO
      ZSNSOABS5(JL,1)=EXP(-ZSNEXTCOEFF5(JL,1)*ZDSN5(1))
      IF (ZSNSOABS5(JL,1) < ZEPSILON) THEN
        ZSNSOABS5(JL,1) = ZEPSILON
      ENDIF

      ZSNOTRSTMP5(1)= PSSRFLTI5(JL,JTILE)*ZSNSOABS5(JL,1)
      ZSNQRAD5(JL,1)=PSSRFLTI5(JL,JTILE)-PSSRFLTI5(JL,JTILE)*(1._JPRB-ZSNSOABS5(JL,1))
      IF (KLACT > 1) THEN
        DO JK=2, KLACT
          ZSNSOABS5(JL,JK)=EXP(-ZSNEXTCOEFF5(JL,JK)*ZDSN5(JK))
          ZSNOTRSTMP5(JK)=ZSNQRAD5(JL,JK-1)*(1._JPRB-ZSNSOABS5(JL,JK))
          ZSNQRAD5(JL,JK)=ZSNQRAD5(JL,JK-1)-ZSNOTRSTMP5(JK)
        ENDDO
      ENDIF
      ZSNOTRSTMP5(KLACT+1)=ZSNQRAD5(JL,KLACT)

      ! Store traj
      IF (JTILE==5) THEN
          ZSNQRAD55(JL,1:KLEVSN)=ZSNQRAD5(JL,1:KLEVSN)
          ZSNSOABS55(JL,1:KLEVSN)=ZSNSOABS5(JL,1:KLEVSN)
      ENDIF
      IF (JTILE==7) THEN
          ZSNQRAD75(JL,1:KLEVSN)=ZSNQRAD5(JL,1:KLEVSN)
          ZSNSOABS75(JL,1:KLEVSN)=ZSNSOABS5(JL,1:KLEVSN)
      ENDIF
  ! Weighted average with tile fraction:
      DO JK=1, KLACT+1
        PSNOTRS5(JL,JK) = PSNOTRS5(JL,JK) + ZFRSN(JL)*ZSNOTRSTMP5(JK)  
      ENDDO
    ENDIF 
  ENDDO
END DO


! 0.  ADJOINT CALCULATIONS
!               --------------------
!

DO JT=1,KSNTILES
  SELECT CASE(JT)
  CASE(1)
    JTILE=5
    ZFRSN(KIDIA:KFDIA)=PFRTI(KIDIA:KFDIA,JTILE)
  CASE(2)
    JTILE=7
    ZFRSN(KIDIA:KFDIA)=PFRTI(KIDIA:KFDIA,JTILE)
  END SELECT
  DO JL=KIDIA,KFDIA
    IF (LLNOSNOW(JL)) THEN 
      PSNOTRS(JL,1:KLEVSN+1) = 0._JPRB

    ELSE
    ! Initialise Adjoint local arrays:
      ZSNOTRSTMP(1:KLEVSN+1) = 0._JPRB
      ZDSN(1:KLEVSN)         = 0._JPRB
      ZSNSOABS(JL,1:KLEVSN)  = 0._JPRB
      ZSNQRAD(1:KLEVSN)      = 0._JPRB

    ! Preparation
      IF ((PFRTI(JL,5)+PFRTI(JL,7))>YDSOIL%RFRTINY) THEN
        ZFRSN_TOT=PFRTI(JL,5)+PFRTI(JL,7)
      ELSE
        ZFRSN_TOT=YDSOIL%RFRTINY
      ENDIF
      DO JK=1,KLEVSN
        IF (PSSNM1M5(JL,JK) > ZEPSILON ) KLACT=JK
      ENDDO

      
      ! Weighted average with tile fraction:
      DO JK=KLACT+1,1,-1
        ZSNOTRSTMP(JK) = ZSNOTRSTMP(JK) + PSNOTRS(JL,JK)*ZFRSN(JL)
      ENDDO
      ZSNQRAD(KLACT)    = ZSNQRAD(KLACT) + ZSNOTRSTMP(KLACT+1)
      ZSNOTRSTMP(KLACT+1) = 0._JPRB
      
      IF (KLACT > 1) THEN
        DO JK=KLACT,2,-1
          ZSNQRAD(JK-1)  = ZSNQRAD(JK-1) + ZSNQRAD(JK)
          ZSNOTRSTMP(JK) = ZSNOTRSTMP(JK)-ZSNQRAD(JK)

          IF (JTILE==5) THEN
            ZSNQRAD(JK-1)   = ZSNQRAD(JK-1) + ZSNOTRSTMP(JK)*(1._JPRB - ZSNSOABS55(JL,JK))
            ZSNSOABS(JL,JK) = ZSNSOABS(JL,JK) - ZSNOTRSTMP(JK)*ZSNQRAD55(JL,JK-1)
          ELSE
            ZSNQRAD(JK-1)   = ZSNQRAD(JK-1) + ZSNOTRSTMP(JK)*(1._JPRB - ZSNSOABS75(JL,JK))
            ZSNSOABS(JL,JK) = ZSNSOABS(JL,JK) - ZSNOTRSTMP(JK)*ZSNQRAD75(JL,JK-1)
          ENDIF
          ZSNOTRSTMP(JK)  = 0._JPRB

          ZDSN(JK)        = ZDSN(JK) - ZSNSOABS(JL,JK)*ZSNEXTCOEFF5(JL,JK)*ZSNSOABS5(JL,JK)
          ZSNSOABS(JL,JK) = 0._JPRB
        ENDDO
      ENDIF

      IF (JTILE==5) THEN
        PSSRFLTI(JL,JTILE) = PSSRFLTI(JL,JTILE) + ZSNQRAD(1)*ZSNSOABS55(JL,1)
      ELSEIF(JTILE==7)THEN
        PSSRFLTI(JL,JTILE) = PSSRFLTI(JL,JTILE) + ZSNQRAD(1)*ZSNSOABS75(JL,1)
      ENDIF
      ZSNSOABS(JL,1)     = ZSNSOABS(JL,1) + ZSNQRAD(1)*PSSRFLTI5(JL,JTILE)
      ZSNQRAD = 0._JPRB

      IF (JTILE==5) THEN
        PSSRFLTI(JL,JTILE) = PSSRFLTI(JL,JTILE) + ZSNOTRSTMP(1)*ZSNSOABS55(JL,1)
      ELSEIF(JTILE==7)THEN
        PSSRFLTI(JL,JTILE) = PSSRFLTI(JL,JTILE) + ZSNOTRSTMP(1)*ZSNSOABS75(JL,1)
      ENDIF
      ZSNSOABS(JL,1)     = ZSNSOABS(JL,1) + ZSNOTRSTMP(1)*PSSRFLTI5(JL,JTILE)
      ZSNOTRSTMP(1)    = 0._JPRB

      IF (JTILE==5) THEN
      IF (ZSNSOABS55(JL,1) < ZEPSILON) THEN
        ZSNSOABS(JL,1)  = 0._JPRB
      ELSE
        ZDSN(1) = ZDSN(1) - ZSNSOABS(JL,1)*ZSNEXTCOEFF5(JL,1)*ZSNSOABS55(JL,1)
        ZSNSOABS(JL,1) = 0._JPRB
      ENDIF
      ENDIF
      IF (JTILE==7) THEN
      IF (ZSNSOABS75(JL,1) < ZEPSILON) THEN
        ZSNSOABS(JL,1)  = 0._JPRB
      ELSE
        ZDSN(1) = ZDSN(1) - ZSNSOABS(JL,1)*ZSNEXTCOEFF5(JL,1)*ZSNSOABS75(JL,1)
        ZSNSOABS(JL,1) = 0._JPRB
      ENDIF
      ENDIF

      DO JK=KLACT,1,-1
        IF (PSSNM1M5(JL,JK)/(ZFRSN_TOT*PRSNM1M5(JL,JK))<RDSNMAX) THEN
          PSSNM1M(JL,JK) = PSSNM1M(JL,JK) + ZDSN(JK)/(ZFRSN_TOT*PRSNM1M5(JL,JK))
          ZDSN(JK)=0._JPRB
        ELSE
          ZDSN(JK)=0._JPRB
        ENDIF
      ENDDO

      ZSNQRAD  = 0._JPRB
      ZSNOTRSTMP(1:KLEVSN+1) = 0._JPRB
!      PSNOTRS(JL,1:KLEVSN+1) = 0._JPRB
      
    ENDIF ! IN LLNOSNOW
  ENDDO   ! IN JL
ENDDO     ! IN JT



END ASSOCIATE
!    -----------------------------------------------------------------
IF (LHOOK) CALL DR_HOOK('SRFSN_SSRABSSAD_MOD:SRFSN_SSRABSSAD',1,ZHOOK_HANDLE)

END SUBROUTINE SRFSN_SSRABSSAD

END MODULE SRFSN_SSRABSSAD_MOD
